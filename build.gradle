plugins {
	id 'com.gradleup.shadow' version '9.3.1'
	id 'maven-publish'
	id 'java'
}

group 'me.crypnotic'
version '3.0.0-SNAPSHOT'
description 'An open-source essential suite for the Velocity proxy'

repositories {
	mavenCentral()

	maven {
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
}

defaultTasks("clean", "build", "shadowJar")

dependencies {
	compileOnly 'com.velocitypowered:velocity-api:3.5.0-SNAPSHOT'
	annotationProcessor 'com.velocitypowered:velocity-api:3.5.0-SNAPSHOT'

	compileOnly 'org.projectlombok:lombok:1.18.42'
	annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
	def props = [
			'id': project.name.toLowerCase(),
			'name': project.name,
			'version': project.version,
			'description': project.description
	]
	task.inputs.properties props

	task.from templateSource
	task.into templateDest
	task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

publishing {
	publications {
	    shadow(MavenPublication) {
			from components.shadow
		}
	}
	
	repositories {
		maven {
			def releasesRepoUrl = System.getenv('mavenUrl')
			def snapshotsRepoUrl = System.getenv('mavenSnapshotUrl')
			
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				
			def mavenUsername = System.getenv('mavenUsername')
			def mavenPassword = System.getenv('mavenPassword')
			if (mavenUsername != null && mavenPassword != null) {
				credentials {
					username = mavenUsername
					password = mavenPassword
				}
			}
		}
	}
} 